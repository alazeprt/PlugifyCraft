plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'top.alazeprt.plugifycraft'
version '1.1'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'top.alazeprt.plugifycraft'
    mainClass = 'top.alazeprt.plugifycraft.PlugifyCraft'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

dependencies {
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.github.alazeprt:PlugifyCraftLib:1.16'
    implementation 'com.jfoenix:jfoenix:9.0.10'
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'PlugifyCraft'
        jvmArgs = ['-Dfile.encoding=UTF-8']
    }

    jpackage {
        appVersion = version.toString()
        icon = file('src/main/resources/top/alazeprt/plugifycraft/icon.ico')
        installerOutputDir = file("$buildDir/installer")
        if(org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            installerOptions.add("--win-dir-chooser")
            installerOptions.add("--win-per-user-install")
            installerOptions.add("--win-menu")
            installerOptions.add("--win-shortcut")
        }
    }

    mergedModule {
        requires('com.google.gson')
    }
}

jlinkZip {
    group = 'distribution'
}